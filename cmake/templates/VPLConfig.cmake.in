# ##############################################################################
# Copyright (C) Intel Corporation
#
# SPDX-License-Identifier: MIT
# ##############################################################################

# * Config file for the VPL package It defines the following variables
#   VPL_<component>_FOUND VPL_IMPORTED_TARGETS
#
# VPLConfigVersion.cmake defines VPL_VERSION

# Initialize to default values
set(VPL_INTERFACE_VERSION @API_VERSION_MAJOR@)

if(NOT VPL_IMPORTED_TARGETS)
  set(VPL_IMPORTED_TARGETS "")
endif()

if(NOT VPL_FIND_COMPONENTS)
  set(VPL_FIND_COMPONENTS "dispatcher;api")
  foreach(_vpl_component ${VPL_FIND_COMPONENTS})
    set(VPL_FIND_REQUIRED_${_vpl_component} 1)
  endforeach()
endif()

if(NOT DEFINED ENV{VPL_LIB})
  message(STATUS "Expected environment variable not defined: VPL_LIB")
endif()
if(NOT DEFINED ENV{VPL_BIN})
  message(STATUS "Expected environment variable not defined: VPL_BIN")
endif()
if(NOT DEFINED ENV{VPL_INCLUDE})
  message(STATUS "Expected environment variable not defined: VPL_INCLUDE")
endif()

# VPL::dispatcher
set(VPL_dispatcher_FOUND 0)
set(vpl_system_name ${CMAKE_SYSTEM_NAME})
if(vpl_system_name STREQUAL "Linux")
  get_filename_component(
    _dispatcher_lib "$ENV{VPL_LIB}/libmfx.so.${VPL_INTERFACE_VERSION}" ABSOLUTE)
elseif(vpl_system_name STREQUAL "Windows")
  get_filename_component(_dispatcher_lib "$ENV{VPL_BIN}/libmfx.dll" ABSOLUTE)
else()
  message(FATAL_ERROR "Unsupported OS name: ${vpl_system_name}")
endif()

if(EXISTS "${_dispatcher_lib}")
  if(NOT TARGET VPL::dispatcher)
    add_library(VPL::dispatcher SHARED IMPORTED)
    set_target_properties(
      VPL::dispatcher PROPERTIES INTERFACE_INCLUDE_DIRECTORIES
                                 "$ENV{VPL_INCLUDE}")
    if(EXISTS "${_dispatcher_lib}")
      set_target_properties(VPL::dispatcher PROPERTIES IMPORTED_LOCATION
                                                       "${_dispatcher_lib}")
      if(vpl_system_name STREQUAL "Windows")
        set_target_properties(
          VPL::dispatcher PROPERTIES IMPORTED_IMPLIB "$ENV{VPL_LIB}/libmfx.lib")
      endif()
    endif()
  endif()
  list(APPEND VPL_IMPORTED_TARGETS VPL::dispatcher)
  set(VPL_dispatcher_FOUND 1)
elseif(VPL_FIND_REQUIRED AND VPL_FIND_REQUIRED_dispatcher)
  message(STATUS "Unable to find required VPL component: dispatcher")
  set(VPL_FOUND FALSE)
endif()

unset(_dispatcher_lib)

# VPL::api
set(VPL_api_FOUND 0)
if(EXISTS $ENV{VPL_INCLUDE})
  if(NOT TARGET VPL::api)
    add_library(VPL::api INTERFACE IMPORTED)
    set_target_properties(VPL::api PROPERTIES INTERFACE_INCLUDE_DIRECTORIES
                                              "$ENV{VPL_INCLUDE}")
  endif()
  list(APPEND VPL_IMPORTED_TARGETS VPL::api)
  set(VPL_api_FOUND 1)
endif()
